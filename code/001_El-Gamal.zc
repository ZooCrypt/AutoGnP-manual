(* CPA secure encryption from the DDH assumption aka El-Gamal. *)

adversary A_ddh : (G * G * G) -> Bool.

assumption ddh ([x,y])
  [ x, y    <-$ Fq; b <- A_ddh(g^x,g^y,g^(x*y)); ]
  [ x, y, t <-$ Fq; b <- A_ddh(g^x,g^y,g^t); ].

adversary A1 : G -> (G * G).
adversary A2 : (G * G) -> Bool.

bound_adv [ 
  a <-$ Fq;
  let PK = g^a;
  (m0,m1) <- A1(PK);
  
  b <-$ Bool;
  let mb = (b?m0:m1);
    
  x <-$ Fq;
  let C1 = g ^x;
  let C2 = mb*g^(a*x);
  b' <- A2(C1,C2);
] : b = b'.
bycrush.

restart.

unfold.
move 4 2.
abstract 3 arg (g^a,g^x,g^(a*x)).
assumption_decisional! ddh -> t.
unfold.
norm_unknown.
move t +2.
rnd t (t -> t - log(b?m0:m1)) _.
norm.
indep!.
qed.